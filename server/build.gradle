plugins {
	id 'java'
	id 'org.springframework.boot' version '2.3.3.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'eclipse'
	id 'jacoco'
	id "io.freefair.lombok" version "5.2.1"
}

group = 'org.owasp.securityshepherd'
version = '4.0.0-testing'
description = 'Security Shepherd'

repositories {
	mavenCentral()
	mavenLocal()
}

sourceSets {
	integrationTest {
		compileClasspath += sourceSets.main.output + sourceSets.test.output
		runtimeClasspath += sourceSets.main.output + sourceSets.test.output

		java.srcDir 'src/integration-test/java'
		resources.srcDir 'src/integration-test/resources'
	}
}

dependencies {
	implementation 'com.google.guava:guava:28.2-jre'
	implementation 'org.springframework.data:spring-data-commons'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'io.r2dbc:r2dbc-h2'
	implementation 'com.github.spotbugs:spotbugs:4.0.1'
	implementation 'org.apache.httpcomponents:httpclient:4.5.12'
	implementation 'org.apache.commons:commons-lang3:3.11'
	implementation 'net.sourceforge.htmlunit:htmlunit:2.39.0'
	implementation 'org.passay:passay:1.0'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'

	implementation 'dev.miku:r2dbc-mysql'
	implementation 'io.jsonwebtoken:jjwt-api:0.11.1'
	implementation 'com.fasterxml.jackson:jackson-bom:2.10.3'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.10.3'
	runtimeOnly 'mysql:mysql-connector-java'
	runtimeOnly 'org.hsqldb:hsqldb:2.5.0'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.1'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.1'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
	testImplementation "org.projectlombok:lombok"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test:5.2.2.RELEASE'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'

	testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.4.2'
	testImplementation 'io.projectreactor:reactor-test:3.3.4.RELEASE'
	testImplementation 'org.apache.commons:commons-lang3:3.11'

	integrationTestCompileOnly 'org.projectlombok:lombok'

	integrationTestAnnotationProcessor "org.projectlombok:lombok"
	integrationTestImplementation "org.projectlombok:lombok"

	integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
	integrationTestImplementation 'org.springframework.security:spring-security-test:5.2.2.RELEASE'
	integrationTestImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
	integrationTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'

	integrationTestImplementation 'nl.jqno.equalsverifier:equalsverifier:3.4.2'
	integrationTestImplementation 'io.projectreactor:reactor-test:3.3.4.RELEASE'
	integrationTestImplementation 'org.apache.commons:commons-lang3:3.11'
}

sourceCompatibility = '1.8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	integrationTestCompile.extendsFrom testCompile
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

task integrationTest(type: Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath

	outputs.upToDateWhen { false }

	shouldRunAfter test
}

integrationTest {
	useJUnitPlatform()

	testLogging {
		events "failed"
		exceptionFormat "full"
	}
}

test {
	useJUnitPlatform()

	testLogging {
		events "failed"
		exceptionFormat "full"
	}
}